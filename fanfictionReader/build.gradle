buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'io.fabric'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.spicymango.fanfictionreader"
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 92
        versionName "1.58"
    }
    lintOptions {
        abortOnError true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        release
    }
    buildTypes {
        // see https://stackoverflow.com/a/40592469
        def addConstantTo = {target, constantName, constantValue ->
            target.manifestPlaceholders += [ (constantName):constantValue]
            target.buildConfigField "String", "${constantName}", "\"${constantValue}\""
        }
        debug{
            applicationIdSuffix ".debug"
            // buildType-specific provider authority
            // 1. manifestPlaceholders is used by AndroidManifest.xml
            // 2. buildConfigField is used by static constant in StoryProvider.java
            // Note: owner refers to current script block
            addConstantTo(owner, "provider_authority", "com.spicymango.fanfictionreader.debug.provider")
            ext.enableCrashlytics = false
        }
        release {
            addConstantTo(owner, "provider_authority", "com.spicymango.fanfictionreader.provider")
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.pro'
            signingConfig signingConfigs.release
        }
    }
    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
    }
}

dependencies {
    implementation project(':kolavar')
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.2'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'org.jsoup:jsoup:1.11.3'
    implementation 'org.apache.commons:commons-collections4:4.2'
    implementation 'org.apache.commons:commons-lang3:3.8.1'
    implementation ('com.crashlytics.sdk.android:crashlytics:2.9.8@aar') {
        transitive = true
    }
}

// If the signing configurations are defined in the local gradle.properties, add them to the script
if (project.hasProperty('key.alias')) {
    android.signingConfigs.release.keyAlias = project.property('key.alias')
    android.signingConfigs.release.keyPassword = project.property('key.alias.password')
    android.signingConfigs.release.storeFile = file(project.property('key.store'))
    android.signingConfigs.release.storePassword = project.property('key.store.password')
}
